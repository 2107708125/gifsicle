AC_INIT(gifsicle.h)

VERSION=1.6
AC_SUBST(VERSION)

AC_PROG_CC
AC_PROG_CPP
AC_C_CONST

AC_PROG_INSTALL
AC_ARG_PROGRAM

build_gifview=
AC_ARG_ENABLE(gifview,
[  --enable-gifview        build gifview, a GIF viewer for X11],
if test "$enableval" = yes ; then
  build_gifview=yes
fi)

build_gifdiff=
AC_ARG_ENABLE(gifdiff,
[  --enable-gifdiff        build gifdiff, a utility for comparing GIFs],
if test "$enableval" = yes ; then
  build_gifdiff=yes
fi)

use_dmalloc=
AC_ARG_ENABLE(dmalloc,
[  --enable-dmalloc        build with debugging malloc library],
if test "$enableval" = yes ; then
  use_dmalloc=yes
fi)

AC_ARG_ENABLE(warn,
[  --enable-warn           build with -Wall],
if test "$enableval" = yes ; then
  CC="$CC -Wall"
fi)

AC_ARG_ENABLE(all,
[  --enable-all            --enable-{gifview + gifdiff + wall}],
if test "$enableval" = yes ; then
  build_gifview=yes
  build_gifdiff=yes
  CC="$CC -Wall"
fi)

dnl
dnl Choose programs to build. Always build gifsicle
dnl

PROGRAMS="gifsicle"

if test "$build_gifview"; then
  PROGRAMS="$PROGRAMS gifview"
  AC_PATH_XTRA
  AC_CACHE_CHECK(for gettimeofday prototype, ac_cv_gettimeofday,
[AC_TRY_COMPILE([#include <time.h>
#include <sys/time.h>],
[gettimeofday((void *)0, (void *)0);],
[AC_TRY_COMPILE([#include <time.h>
#include <sys/time.h>],
[gettimeofday((void *)0);],
ac_cv_gettimeofday=0,
ac_cv_gettimeofday=2)],
ac_cv_gettimeofday=1)])
  if test $ac_cv_gettimeofday = 1 ;
  then GETTIMEOFDAY='-DSYSV_GETTIMEOFDAY'
  elif test $ac_cv_gettimeofday = 0 ;
  then GETTIMEOFDAY='-DNO_GETTIMEOFDAY_PROTO'
  fi
fi

if test "$build_gifdiff"; then
  PROGRAMS="$PROGRAMS gifdiff"
fi

INSTALLERS=[`echo $PROGRAMS | sed 's/\([a-z][a-z]*\)/install-\1/g'`]
AC_SUBST(PROGRAMS)dnl
AC_SUBST(INSTALLERS)dnl
AC_SUBST(GETTIMEOFDAY)dnl

dnl
dnl Set up malloc library
dnl

if test "$use_dmalloc"; then
  GIF_MEMORY_DEFINES='#include "dmalloc.h"\
#define Gif_DeleteFunc		(&debug_free)\
#define Gif_DeleteArrayFunc	(&debug_free)'
  AC_DEFINE(DMALLOC)
  MALLOC_O='dmalloc.o'
else
  GIF_MEMORY_DEFINES='#define malloc(s)		fail_die_malloc((s),__FILE__,__LINE__)\
#define realloc(p,s)		fail_die_realloc((p),(s),__FILE__,__LINE__)\
void *fail_die_malloc(int, const char *, int);\
void *fail_die_realloc(void *, int, const char *, int);'
  MALLOC_O='fmalloc.o'
fi
AC_SUBST(GIF_MEMORY_DEFINES)dnl
AC_SUBST(MALLOC_O)dnl

dnl
dnl random() or rand()?
dnl

AC_CHECK_FUNC(random, RANDOM_FUNC=random, RANDOM_FUNC=rand)
AC_SUBST(RANDOM_FUNC)dnl

dnl
dnl Integer typedefs
dnl

AC_CACHE_CHECK(for integer typedefs, ac_cv_int_types,
[AC_EGREP_HEADER(u_int32_t, sys/types.h, ac_cv_int_types=yes,
  AC_EGREP_HEADER(uint32_t, sys/types.h, ac_cv_int_types=close,
   ac_cv_int_types=no))])

if test "$ac_cv_int_types" = no ;
then
  # Define our own versions
  AC_CACHE_CHECK(for a 16-bit value, ac_cv_int16,
  [AC_TRY_RUN([#include <stdio.h>
#include <stdlib.h>
int main(int argc, char **argv) {
  FILE *f = fopen("conftestval", "w");
  if (!f) exit(1);
  if (sizeof(short) == 2) fprintf(f, "short\n");
  else exit(1);
  fclose(f);
  exit(0);
}], ac_cv_int16=`cat conftestval`,
  AC_MSG_ERROR(no 16-bit value found),
  ac_cv_int16=short)])

  AC_CACHE_CHECK(for a 32-bit value, ac_cv_int32,
  [AC_TRY_RUN([#include <stdio.h>
#include <stdlib.h>
int main(int argc, char **argv) {
  FILE *f = fopen("conftestval", "w");
  if (!f) exit(1);
  if (sizeof(int) == 4) fprintf(f, "int\n");
  else if (sizeof(long) == 4) fprintf(f, "long\n");
  else exit(1);
  fclose(f);
  exit(0);
}], ac_cv_int32=`cat conftestval`, 
  AC_MSG_ERROR(no 32-bit value found),
  ac_cv_int32=int)])
fi

if test "$ac_cv_int_types" = no ;
then
  INTEGER_TYPES="typedef unsigned $ac_cv_int16 u_int16_t;\\
typedef unsigned $ac_cv_int32 u_int32_t;\\
typedef $ac_cv_int32 int32_t;"

elif test "$ac_cv_int_types" = close ;
then
  INTEGER_TYPES="#include <sys/types.h>\\
typedef uint16_t u_int16_t;\\
typedef uint32_t u_int32_t;"

else
  INTEGER_TYPES="#include <sys/types.h>"
fi
AC_SUBST(INTEGER_TYPES)dnl

dnl
dnl Output
dnl

AC_OUTPUT(Makefile gif.h)
