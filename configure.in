AC_INIT(gifsicle.h)

VERSION=1.3.1
AC_SUBST(VERSION)
GIFDIFFVERSION=1.0
AC_SUBST(GIFDIFFVERSION)

AC_PROG_CC
if test -n "$GCC" ; then CC="$CC -Wall" ; CXX="$CXX -Wall" ; fi
AC_C_CONST

AC_PROG_INSTALL

AC_PROG_CPP

build_gifview=
AC_ARG_ENABLE(gifview,
[  --enable-gifview        build gifview, a GIF viewer for X11],
if test "$enableval" = yes ; then
  build_gifview=yes
fi)

build_gifdiff=
AC_ARG_ENABLE(gifdiff,
[  --enable-gifdiff        build gifdiff, a utility for comparing GIFs],
if test "$enableval" = yes ; then
  build_gifdiff=yes
fi)

use_dmalloc=
AC_ARG_ENABLE(dmalloc,
[  --enable-dmalloc        build with debugging malloc library],
if test "$enableval" = yes ; then
  use_dmalloc=yes
fi)

# Choose programs to build. Always build gifsicle
PROGRAMS="gifsicle"

if test "$build_gifview"; then
  PROGRAMS="$PROGRAMS gifview"
  AC_PATH_XTRA
  AC_CACHE_CHECK(for gettimeofday prototype, ac_cv_gettimeofday,
[AC_TRY_COMPILE([#include <time.h>
#include <sys/time.h>],
[gettimeofday((void *)0, (void *)0);],
[AC_TRY_COMPILE([#include <time.h>
#include <sys/time.h>],
[gettimeofday((void *)0);],
ac_cv_gettimeofday=0,
ac_cv_gettimeofday=2)],
ac_cv_gettimeofday=1)])
  if test $ac_cv_gettimeofday = 1 ;
  then GETTIMEOFDAY='-DSYSV_GETTIMEOFDAY'
  elif test $ac_cv_gettimeofday = 0 ;
  then GETTIMEOFDAY='-DNO_GETTIMEOFDAY_PROTO'
  fi
fi

if test "$build_gifdiff"; then
  PROGRAMS="$PROGRAMS gifdiff"
fi

INSTALLERS=[`echo $PROGRAMS | sed 's/\([a-z][a-z]*\)/install-\1/g'`]
AC_SUBST(PROGRAMS)dnl
AC_SUBST(INSTALLERS)dnl
AC_SUBST(GETTIMEOFDAY)dnl

# Set up malloc library
if test "$use_dmalloc"; then
  MEMORY_PREPROCESSOR='#include "dmalloc.h"\
#define Gif_DeleteFunc		(&debug_free)\
#define Gif_DeleteArrayFunc	(&debug_free)'
  AC_DEFINE(DMALLOC)
  MALLOC_O='dmalloc.o'
else
  MEMORY_PREPROCESSOR='#define malloc(s)		fail_die_malloc((s),__FILE__,__LINE__)\
#define realloc(p,s)		fail_die_realloc((p),(s),__FILE__,__LINE__)\
void *fail_die_malloc(int, const char *, int);\
void *fail_die_realloc(void *, int, const char *, int);'
  MALLOC_O='fmalloc.o'
fi
AC_SUBST(MEMORY_PREPROCESSOR)dnl
AC_SUBST(MALLOC_O)dnl

AC_CACHE_CHECK(for integer typedefs, ac_cv_int_types,
[AC_EGREP_HEADER(u_int32_t, sys/types.h, ac_cv_int_types=sys/types.h,
  AC_EGREP_HEADER(u_int32_t, sys/int_types.h, ac_cv_int_types=sys/int_types.h,
   AC_EGREP_HEADER(u_int32_t, sys/_inttypes.h, ac_cv_int_types=sys/_inttypes.h,
    ac_cv_int_types=no)))])

if test "$ac_cv_int_types" = no ;
then
  # Define our own versions
  AC_CACHE_CHECK(for a 16-bit value, ac_cv_int16,
  [AC_TRY_RUN([#include <stdio.h>
#include <stdlib.h>
int main(int argc, char **argv) {
  FILE *f = fopen("conftestval", "w");
  if (!f) exit(1);
  if (sizeof(short) == 2) fprintf(f, "short\n");
  else exit(1);
  fclose(f);
  exit(0);
}], ac_cv_int16=`cat conftestval`,
  AC_MSG_ERROR(no 16-bit value found),
  ac_cv_int16=short)])

  AC_CACHE_CHECK(for a 32-bit value, ac_cv_int32,
  [AC_TRY_RUN([#include <stdio.h>
#include <stdlib.h>
int main(int argc, char **argv) {
  FILE *f = fopen("conftestval", "w");
  if (!f) exit(1);
  if (sizeof(int) == 4) fprintf(f, "int\n");
  else if (sizeof(long) == 4) fprintf(f, "long\n");
  else exit(1);
  fclose(f);
  exit(0);
}], ac_cv_int32=`cat conftestval`, 
  AC_MSG_ERROR(no 32-bit value found),
  ac_cv_int32=int)])
  
  INTEGER_TYPES="typedef unsigned $ac_cv_int16 u_int16_t;\\
typedef $ac_cv_int32 int32_t;\\
typedef unsigned $ac_cv_int32 u_int32_t;"

else
  # Found int typedefs in a system header file
  INTEGER_TYPES="#include <$ac_cv_int_types>"
fi
AC_SUBST(INTEGER_TYPES)dnl

AC_OUTPUT(Makefile gif.h)
